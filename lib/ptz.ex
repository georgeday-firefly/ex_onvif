defmodule ExOnvif.PTZ do
  @moduledoc """
  Interface for making requests to the Onvif PTZ(Pan/Tilt/Zoom) service

  https://www.onvif.org/onvif/ver20/ptz/wsdl/ptz.wsdl
  """

  import ExOnvif.Utils.ApiClient, only: [ptz_request: 4]
  import ExOnvif.Utils.XmlBuilder
  import ExOnvif.Utils.Parser
  import SweetXml

  alias ExOnvif.PTZ.{
    AbsoluteMove,
    ContinuousMove,
    Node,
    Preset,
    ServiceCapabilities,
    Status,
    Stop,
    Vector
  }

  @type profile_token :: String.t()

  @doc """
  Operation to move pan,tilt or zoom to a absolute destination.

  The speed argument is optional. If an x/y speed value is given it is up to the device to either use the x value as absolute resoluting
  speed vector or to map x and y to the component speed. If the speed argument is omitted, the default speed set by the
  PTZConfiguration will be used.
  """
  @spec absolute_move(ExOnvif.Device.t(), AbsoluteMove.t()) :: :ok | ExOnvif.error()
  def absolute_move(device, abs_move) do
    body = AbsoluteMove.encode(abs_move)
    ptz_request(device, "AbsoluteMove", body, fn _body -> :ok end)
  end

  @doc """
  Operation for continuous Pan/Tilt and Zoom movements.

  The operation is supported if the PTZNode supports at least one continuous Pan/Tilt or Zoom space.
  If the space argument is omitted, the default space set by the PTZConfiguration will be used.
  """
  @spec continuous_move(ExOnvif.Device.t(), ContinuousMove.t()) :: :ok | ExOnvif.error()
  def continuous_move(device, continuous_move) do
    body = ContinuousMove.encode(continuous_move)
    ptz_request(device, "ContinuousMove", body, fn _body -> :ok end)
  end

  @doc """
  Operation to stop ongoing pan, tilt and zoom movements of absolute, relative and continuous type.

  If no stop argument for pan, tilt or zoom is set, the device will stop all ongoing pan, tilt and zoom movements.
  """
  @spec stop(ExOnvif.Device.t(), Stop.t()) :: :ok | ExOnvif.error()
  def stop(device, stop) do
    body = Stop.encode(stop)
    ptz_request(device, "Stop", body, fn _body -> :ok end)
  end

  @doc """
  Get a specific PTZ Node identified by a reference token or a name.
  """
  @spec get_node(ExOnvif.Device.t(), String.t()) :: {:ok, Node.t()} | ExOnvif.error()
  def get_node(device, node_token) do
    body = element("tptz:GetNode", element("tptz:NodeToken", node_token))
    ptz_request(device, "GetNode", body, &parse_node_response/1)
  end

  @doc """
  Get the descriptions of the available PTZ Nodes.

  A PTZ-capable device may have multiple PTZ Nodes. The PTZ Nodes may represent mechanical PTZ drivers, uploaded PTZ drivers or digital PTZ drivers.
  PTZ Nodes are the lowest level entities in the PTZ control API and reflect the supported PTZ capabilities. The PTZ Node is referenced
  either by its name or by its reference token.
  """
  @spec get_nodes(ExOnvif.Device.t()) :: {:ok, [Node.t()]} | ExOnvif.error()
  def get_nodes(device) do
    ptz_request(device, "GetNodes", :"tptz:GetNodes", &parse_nodes_response/1)
  end

  @doc """
  Returns the capabilities of the PTZ service.
  """
  @spec get_service_capabilities(ExOnvif.Device.t()) ::
          {:ok, ServiceCapabilities.t()} | ExOnvif.error()
  def get_service_capabilities(device) do
    body = :"tptz:GetServiceCapabilities"
    ptz_request(device, "GetServiceCapabilities", body, &parse_service_capabilities/1)
  end

  @doc """
  Returns the the PTZconfigurations
  Get all the existing PTZConfigurations from the device.

  The default Position/Translation/Velocity Spaces are introduced to allow NVCs sending move requests without the need to specify a
  certain coordinate system. The default Speeds are introduced to control the speed of move requests (absolute, relative, preset),
  where no explicit speed has been set.

  """
  @spec get_configurations(ExOnvif.Device.t(), profile_token()) ::
          {:ok, [Configurations.t()]} | ExOnvif.error()
  def get_configurations(device, profile_token) do
    body = element("tptz:GetConfigurations", element("tptz:ProfileToken", profile_token))
    ptz_request(device, "GetConfigurations", body, &parse_configuration_response/1)
  end

  @doc """
  Operation to request PTZ status for the Node in the selected profile.
  """
  @spec get_status(ExOnvif.Device.t(), String.t()) :: {:ok, Status.t()} | ExOnvif.error()
  def get_status(device, profile_token) do
    body = element("tptz:GetStatus", element("tptz:ProfileToken", profile_token))
    ptz_request(device, "GetStatus", body, &parse_status_response/1)
  end

  @doc """
  Operation to save current position as the home position.
  The SetHomePosition command returns with a failure if the “home” position is fixed and cannot be overwritten.
  If the SetHomePosition is successful, it is possible to recall the Home Position with the GotoHomePosition command.
  """
  @spec set_home_position(ExOnvif.Device.t(), String.t()) :: :ok | ExOnvif.error()
  def set_home_position(device, profile_token) do
    body = element("tptz:SetHomePosition", element("tptz:ProfileToken", profile_token))
    ptz_request(device, "SetHomePosition", body, fn _body -> :ok end)
  end

  @doc """
  Operation to move the PTZ device to it's "home" position. The operation is supported if the HomeSupported element in the PTZNode is true.
  """
  @spec goto_home_position(ExOnvif.Device.t(), String.t(), Vector.t() | nil) :: :ok | ExOnvif.error()
  def goto_home_position(device, profile_token, speed \\ nil) do
    body =
      element("tptz:Speed", speed && Vector.encode(speed))
      |> element("tptz:ProfileToken", profile_token)
      |> then(&element("tptz:GotoHomePosition", &1))

    ptz_request(device, "GotoHomePosition", body, fn _body -> :ok end)
  end

  @doc """
  Operation to request all PTZ presets for the PTZNode in the selected profile.
  The operation is supported if there is support for at least on PTZ preset by the PTZNode.
  """
  @spec get_presets(ExOnvif.Device.t(), profile_token()) :: [Preset.t()] | ExOnvif.error()
  def get_presets(device, profile_token) do
    body = element("tptz:GetPresets", element("tptz:ProfileToken", profile_token))
    ptz_request(device, "GetPresets", body, &parse_presets/1)
  end

  @type set_preset_options :: [{:name, String.t()} | {:token, String.t()}]

  @doc """

  The SetPreset command saves the current device position parameters so that the device can move to the saved preset position through the GotoPreset operation.
  In order to create a new preset, the SetPresetRequest contains no PresetToken.
  If creation is successful, the Response contains the PresetToken which uniquely identifies the Preset.
  An existing Preset can be overwritten by specifying the PresetToken of the corresponding Preset. In both cases (overwriting or creation) an optional PresetName can be specified. The operation fails if the PTZ device is moving during the SetPreset operation.
  The device MAY internally save additional states such as imaging properties in the PTZ Preset which then should be recalled in the GotoPreset operation.
  """
  @spec set_preset(ExOnvif.Device.t(), profile_token(), set_preset_options()) ::
          {:ok, String.t()} | ExOnvif.error()
  def set_preset(device, profile_token, options \\ []) do
    body =
      element("tptz:SetPreset", [
        element("tptz:ProfileToken", profile_token),
        element("tptz:PresetName", options[:name]),
        element("tptz:PresetToken", options[:token])
      ])

    ptz_request(device, "SetPreset", body, &parse_set_preset/1)
  end

  @doc """
  Operation to remove a PTZ preset for the Node in the selected profile.
  The operation is supported if the PresetPosition capability exists for the Node in the selected profile.
  """
  @spec remove_preset(ExOnvif.Device.t(), profile_token(), String.t()) :: {:ok, String.t()} | ExOnvif.error()
  def remove_preset(device, profile_token, preset_token) do
    body =
      element("tptz:RemovePreset", [
        element("tptz:ProfileToken", profile_token),
        element("tptz:PresetToken", preset_token)
      ])

    ptz_request(device, "RemovePreset", body, fn _body -> :ok end)
  end

  @doc """
  Operation to go to a saved preset position for the PTZNode in the selected profile. The operation is supported if there is support for at least on PTZ preset by the PTZNode.
  """
  @spec goto_preset(ExOnvif.Device.t(), profile_token(), String.t(), Vector.t() | nil) :: :ok | ExOnvif.error()
  def goto_preset(device, profile_token, preset_token, move \\ nil) do
    body =
      element("tptz:Speed", move && Vector.encode(move))
      |> element("tptz:PresetToken", preset_token)
      |> element("tptz:ProfileToken", profile_token)
      |> then(&element("tptz:GotoPreset", &1))

    ptz_request(device, "GotoPreset", body, fn _body -> :ok end)
  end

  defp parse_configuration_response(xml_response_body) do
    xml_response_body
    |> parse(namespace_conformant: true, quiet: true)
    |> xpath(
      ~x"//s:Envelope/s:Body/tptz:GetConfigurationsResponse/tptz:PTZConfiguration"e
      |> add_namespace("s", "http://www.w3.org/2003/05/soap-envelope")
      |> add_namespace("tt", "http://www.onvif.org/ver10/schema")
      |> add_namespace("tptz", "http://www.onvif.org/ver20/ptz/wsdl")
    )
    |> ExOnvif.PTZ.Configurations.parse()
    |> ExOnvif.PTZ.Configurations.to_struct()
  end

  defp parse_set_preset(xml_response_body) do
    xml_response_body
    |> parse(namespace_conformant: true, quiet: true)
    |> xpath(
      ~x"//s:Envelope/s:Body/tptz:SetPresetResponse/tptz:PresetToken/text()"s
      |> add_namespace("s", "http://www.w3.org/2003/05/soap-envelope")
      |> add_namespace("tptz", "http://www.onvif.org/ver20/ptz/wsdl")
    )
    |> then(&{:ok, &1})
  end

  defp parse_presets(xml_response_body) do
    xml_response_body
    |> parse(namespace_conformant: true, quiet: true)
    |> xpath(
      ~x"//s:Envelope/s:Body/tptz:GetPresetsResponse/tptz:Preset"el
      |> add_namespace("s", "http://www.w3.org/2003/05/soap-envelope")
      |> add_namespace("tt", "http://www.onvif.org/ver10/schema")
      |> add_namespace("tptz", "http://www.onvif.org/ver20/ptz/wsdl")
    )
    |> parse_map_reduce(Preset)
  end

  defp parse_node_response(xml_response_body) do
    xml_response_body
    |> parse(namespace_conformant: true, quiet: true)
    |> xpath(
      ~x"//s:Envelope/s:Body/tptz:GetNodeResponse/tptz:PTZNode"e
      |> add_namespace("s", "http://www.w3.org/2003/05/soap-envelope")
      |> add_namespace("tt", "http://www.onvif.org/ver10/schema")
      |> add_namespace("tptz", "http://www.onvif.org/ver20/ptz/wsdl")
    )
    |> Node.parse()
    |> Node.to_struct()
  end

  defp parse_nodes_response(xml_response_body) do
    xml_response_body
    |> parse(namespace_conformant: true, quiet: true)
    |> xpath(
      ~x"//s:Envelope/s:Body/tptz:GetNodesResponse/tptz:PTZNode"el
      |> add_namespace("s", "http://www.w3.org/2003/05/soap-envelope")
      |> add_namespace("tt", "http://www.onvif.org/ver10/schema")
      |> add_namespace("tptz", "http://www.onvif.org/ver20/ptz/wsdl")
    )
    |> parse_map_reduce(Node)
  end

  defp parse_service_capabilities(xml_response_body) do
    xml_response_body
    |> parse(namespace_conformant: true, quiet: true)
    |> xpath(
      ~x"//s:Envelope/s:Body/tptz:GetServiceCapabilitiesResponse/tptz:Capabilities"e
      |> add_namespace("s", "http://www.w3.org/2003/05/soap-envelope")
      |> add_namespace("tt", "http://www.onvif.org/ver10/schema")
      |> add_namespace("tptz", "http://www.onvif.org/ver20/ptz/wsdl")
    )
    |> ServiceCapabilities.parse()
    |> ServiceCapabilities.to_struct()
  end

  defp parse_status_response(xml_response_body) do
    xml_response_body
    |> parse(namespace_conformant: true, quiet: true)
    |> xpath(
      ~x"//s:Envelope/s:Body/tptz:GetStatusResponse/tptz:PTZStatus"e
      |> add_namespace("s", "http://www.w3.org/2003/05/soap-envelope")
      |> add_namespace("tt", "http://www.onvif.org/ver10/schema")
      |> add_namespace("tptz", "http://www.onvif.org/ver20/ptz/wsdl")
    )
    |> Status.parse()
    |> Status.to_struct()
  end
end
